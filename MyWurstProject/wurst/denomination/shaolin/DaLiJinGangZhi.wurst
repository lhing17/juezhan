package DaLiJinGangZhi

import AbilityIdConstant
import Common
import KongFuLevelUp
import ClosureForGroups
import Formula
import Hero
import UniversalBuff


function isDali() returns bool
    return GetSpellAbilityId() == DA_LI_JIN_GANG_ZHI and GetTriggerUnit().isType(UNIT_TYPE_HERO)

function daLiDamage(unit source, unit target)
    var damageCoeff = 1.0
    if source.hasAbility(TAN_ZHI_SHEN_TONG)
        damageCoeff += 0.8
    if source.hasAbility(JIU_YANG_SHEN_GONG)
        damageCoeff += 0.6
    if source.hasAbility(XI_XING_DA_FA) and source.hasAbility(HUA_GONG_DA_FA)
        damageCoeff += 1.0
    if source.hasItemById('I0AL')
        damageCoeff *= 8
    real damage
    if source.hasAbility(JIU_YIN_NEI_GONG)
        damage = calculateDamage(source, target, 200, 200, damageCoeff * source.getHero().criticalTimes, DA_LI_JIN_GANG_ZHI)
    else
        damage = calculateDamage(source, target, 200, 200, damageCoeff, DA_LI_JIN_GANG_ZHI)
    applyDamage(source, target, damage)
    if source.getHero().constitution >= 20 and not(source.hasAbility(FENG_XUE))
        if source.hasAbility(YI_YANG_ZHI)
            applyUniversalBuff(source, target, BUFF_DEEP_DIZZY)
        else
            applyUniversalBuff(source, target, BUFF_DIZZY)
    if source.getHero().perception >= 20 and target.getHP() <= 0.2 * target.getMaxHP() and percentPossiblity(50)
        target.setHP(1)
        applyDamage(source, target, damage * 1000)
        createTTEx(target.getPos3Real(), vec2(100, 90), "斩杀", 16, 3, colorA(65, 55, 42, 0))
    if source.hasAbility(XI_XING_DA_FA) and source.hasAbility(HUA_GONG_DA_FA) and not(source.hasAbility(SHEN_JING_CUO_LUAN))
        applyUniversalBuff(source, target, BUFF_INSANE)

function daLi()
    let u = GetTriggerUnit()
    let ut = GetSpellTargetUnit()
    kongFuLevelUp(u, DA_LI_JIN_GANG_ZHI, 160)
    if u.hasAbility(KUI_HUA_BAO_DIAN) and u.hasAbility(LUO_HUA_FU_MO_GONG)
        forUnitsInRange(u.getPos(), 500) ue ->
            if ue.isEnemyOf(u) and ue.isAlive()
                daLiDamage(u, ue)
    else
        daLiDamage(u, ut)

init
    CreateTrigger()
        ..registerAnyUnitEvent(EVENT_PLAYER_UNIT_SPELL_EFFECT)
        ..addCondition(Condition(function isDali))
        ..addAction(function daLi)