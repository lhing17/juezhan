package XiangMoGong

import AbilityIdConstant
import Hero
import Common
import KongFuLevelUp
import TriggerEvent
import Formula

int array xiangmo_killed_unit_nums
int array xiangmo_attacked_nums

function isXiangMoStatBonus() returns bool
    return GetKillingUnit().hasAbility(XIANG_MO_GONG)

function xiangMoStatBonus()
    let i = GetKillingUnit().getOwner().getId()
    xiangmo_killed_unit_nums[i]++
    if xiangmo_killed_unit_nums[i] >= 10
        xiangmo_killed_unit_nums[i] -= 10
        GetKillingUnit().addInt(1)


function isXiangMoAttacked() returns bool
    return GetTriggerUnit().hasAbility(XIANG_MO_GONG)

function xiangMoAttacked()
    let u = GetTriggerUnit()
    let i = u.getOwner().getId()
    if u.hasAbility(SHEN_ZHAO_JING)
        xiangmo_attacked_nums[i]++
        if xiangmo_attacked_nums[i] >= 10
            xiangmo_attacked_nums[i] -= 10
            for j = 1 to 18
                dummyUnitCastAbilityPos(u.getOwner(), DUMMY_XIANG_MO, 1, $D00FA, u.getPos(), u.getPos().polarOffset((20. * j).asAngleDegrees(), 256))
    if percentPossiblity(15 + u.getHero().luck / 5)
        kongFuLevelUp(u, XIANG_MO_GONG, 6000)

function isXiangMoAttacking() returns bool
    return GetAttacker().hasAbility(XIANG_MO_GONG)

function xiangMoAttacking()
    let u = GetAttacker()
    let ut = GetTriggerUnit()
    if percentPossiblity(2) and u.hasAbility(JIU_YANG_SHEN_GONG) and u.hasAbility(JIU_YIN_NEI_GONG)
        createUnit(u.getOwner(), 'npn2', ut.getPos(), bj_UNIT_FACING.asAngleDegrees()).setTimedLife(180)
    if u.hasAbility(LONG_XIANG_BO_RE_GONG)
        if ut.getHP() < ut.getMaxHP() * 0.01
            ut.setHP(1)
        else
            ut.setHP(ut.getHP() - ut.getMaxHP() * 0.01)

function isXiangMoDamage() returns bool
    return GetEventDamage() == 0.18

function xiangMoDamage()
    let hero = GetEventDamageSource().getHero()
    var damageCoeff = 1.0
    if hero.u.hasItemById('I0AL')
        damageCoeff *= 8
    var damage = calculateDamage(hero.u, GetTriggerUnit(), 50, 50, damageCoeff, XIANG_MO_GONG)
    applyDamage(hero.u, GetTriggerUnit(), damage)


init
    CreateTrigger()
        ..registerAnyUnitEvent(EVENT_PLAYER_UNIT_DEATH)
        ..addCondition(Condition(function isXiangMoStatBonus))
        ..addAction(function xiangMoStatBonus)

    CreateTrigger()
        ..registerAnyUnitEvent(EVENT_PLAYER_UNIT_ATTACKED)
        ..addCondition(Condition(function isXiangMoAttacked))
        ..addAction(function xiangMoAttacked)
    
    CreateTrigger()
        ..registerAnyUnitEvent(EVENT_PLAYER_UNIT_ATTACKED)
        ..addCondition(Condition(function isXiangMoAttacking))
        ..addAction(function xiangMoAttacking)
    
    CreateTrigger()
        ..registerAnyUnitDamagedEvent()
        ..addCondition(Condition(function isXiangMoDamage))
        ..addAction(function xiangMoDamage)

