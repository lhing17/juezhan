package InstanceInitializer

// standard imports
import Event
import HashList
import ClosureTimers

// custom imports
import Common
import GameConfiguration
import GlobalInitialization
import Lists
import Hero
import ColorUtils

/** 副本类 **/
public class Instance
    /** 触发进入副本动作的物品 **/
    int itemId
    /** 副本名称 **/
    string name
    /** 副本序号，从0开始 **/
    int index
    /** 神秘怪物的unitId列表 **/
    HashList<int> mysteriousMonsterIds
    /** 储存实际产生的神秘怪物 **/
    unit mysteriousMonster
    /** 稀有掉落的itemId列表 **/
    HashList<int> rareDropItemIds
    /** 某次刷新实际产生的稀有掉落 **/
    int rareDropItemId
    /** 奇缘NPC的unitId列表 **/
    HashList<int> fateNpcIds
    /** 某次刷新实际产生的奇缘NPC **/
    unit fateNpc
    /** 刷新副本的定时器 **/
    timer refreshTimer
    /** 是否需要刷新副本 **/
    boolean refreshFlag = true
    /** 小怪的unitId **/
    HashList<int> creepIds
    /** BOSS的unitId，不包括神秘怪物 **/
    HashList<int> bossIds
    /** 小怪武功 **/
    int creepAbilityId
    /** boss武功 **/
    int bossAbilityId
    /** 进入副本所需的历练数 **/
    int practiceRequired
    /** 进入副本所需的金钱数 **/
    int goldRequired
    /** 进入副本所需的珍稀币数 **/
    int lumberRequired
    /** 进入副本的着陆点 **/
    vec2 landPos
    /** 刷神秘怪物地点 **/
    vec2 mysteriousMonsterPos
    /** 刷奇缘NPC地点 **/
    vec2 fateNpcPos
    construct(int itemId, int index, string name, int practiceRequired, int goldRequired, int lumberRequired, vec2 landPos)
        this.itemId = itemId
        this.index = index
        this.name = name
        this.practiceRequired = practiceRequired
        this.goldRequired = goldRequired
        this.lumberRequired = lumberRequired
        this.landPos = landPos
        refreshTimer = CreateTimer()
        EventListener.add(EVENT_PLAYER_UNIT_PICKUP_ITEM, () -> initializeInstances())

    /**
    * 传送至副本并进行初始化 
    * 1. 载入神秘怪物
    * 2. 载入稀有掉落
    * 3. 载入奇缘NPC
    **/        
    function initializeInstances()
        let p = GetTriggerUnit().getOwner()
        let hero = p.getHero()
        if GetManipulatedItem().getTypeId() == this.itemId
            if hero.practice < this.index
                p.addGold(this.goldRequired)
                p.addLumber(this.lumberRequired)
                printTimedToPlayer(ColorUtils.WARNING + "修行达到" + (this.index + 1).toString() + "才可以进入，你的修行不足，请前往修行去吧", 15, p)
                return
            if not GetTriggerUnit().isHero()
                p.addGold(this.goldRequired)
                p.addLumber(this.lumberRequired)
                printTimedToPlayer(ColorUtils.WARNING + "只能由主角亲自购买前往", 15, p)
                return
            GetTriggerUnit().setPos(this.landPos)
            p.panCamTo(GetTriggerUnit())
            if refreshFlag
                refreshFlag = false
                refreshTimer.start(150, null)
                doAfter(150) ->
                    refreshFlag = true
                printTimedToPlayer("|cFFFFCC00开始载入动态副本：|r |cFF99FFCC" + this.name + "|r", 30, p)
                printTimedToPlayer("|cFFcc99ff――――――――――", 30, p)
                loadMysteriousMonster(p)
                loadRareDropItem(p)
                loadFateNpc(p)
                printTimedToPlayer("|cFFFFCC33小怪武功：|cFF33FFFF暂未制作此玩法", 30, p)
                printTimedToPlayer("|cFFFFCC33BOSS武功：|cFF33FFFF暂未制作此玩法", 30, p)
                printTimedToPlayer("|cFFFF0033副本重置倒计时：|cFF33FFFF" + refreshTimer.getRemaining().toInt().toString() + "秒", 30, p)
            else
                printTimedToPlayer("|cFFFFCC00进入动态副本：|r |cFF99FFCC" + name + "|r", 30, p)
                printTimedToPlayer("|cFFcc99ff――――――――――", 30, p)
                printTimedToPlayer("|cFFFFCC33神秘怪物：|cFF33FFFF" + mysteriousMonster.getName(), 30, p)
                printTimedToPlayer("|cFFFFCC33稀有掉落：|cFF33FFFF" + GetObjectName(rareDropItemId), 30, p)
                printTimedToPlayer("|cFFFFCC33奇缘NPC：|cFF33FFFF" + fateNpc.getName(), 30, p)
                printTimedToPlayer("|cFFFFCC33小怪武功：|cFF33FFFF暂未制作此玩法", 30, p)
                printTimedToPlayer("|cFFFFCC33BOSS武功：|cFF33FFFF暂未制作此玩法", 30, p)
                printTimedToPlayer("|cFFFF0033副本重置倒计时：|cFF33FFFF" + refreshTimer.getRemaining().toInt().toString() + "秒", 30, p)
    
    private function loadMysteriousMonster(player p)
        if percentPossiblity(20)
            printTimedToPlayer("|cFFFFCC33载入神秘怪物：|cFF33FFFF无", 30, p)
        else
            mysteriousMonster.remove()
            let unitId = mysteriousMonsterIds.get(GetRandomInt(0, mysteriousMonsterIds.size() - 1))
            mysteriousMonster = createUnit(CREEP_PLAYER, unitId, mysteriousMonsterPos, DEFAULT_FACING)
            printTimedToPlayer("|cFFFFCC33载入神秘怪物：|cFF33FFFF" + mysteriousMonster.getName(), 30, p)

    private function loadRareDropItem(player p)
        if percentPossiblity(20)
            printTimedToPlayer("|cFFFFCC33载入稀有掉落：|cFF33FFFF无", 30, p)
        else
            rareDropItemId = rareDropItemIds.get(GetRandomInt(0, rareDropItemIds.size() - 1))
            printTimedToPlayer("|cFFFFCC33载入稀有掉落：|cFF33FFFF" + GetObjectName(rareDropItemId), 30, p)
    
    private function loadFateNpc(player p)
        if percentPossiblity(50)
            printTimedToPlayer("|cFFFFCC33载入奇缘NPC：|cFF33FFFF无", 30, p)
        else
            fateNpc.remove()
            let unitId = fateNpcIds.get(GetRandomInt(0, fateNpcIds.size() - 1))
            fateNpc = createUnit(NEUTRAL_NPC_PLAYER, unitId, fateNpcPos, DEFAULT_FACING)
            printTimedToPlayer("|cFFFFCC33载入奇缘NPC：|cFF33FFFF" + fateNpc.getName(), 30, p)


init
    // TESTME
    // 副本1 马贼洞
    Instance instance0 = new Instance('I02F', 0, "马贼洞", 0, 100, 0, vec2(-11900, 630)) 
    instance0.mysteriousMonsterIds = Lists.newHashList('otau', 'odkt', 'uaco')
    instance0.rareDropItemIds = Lists.newHashList('I021', 'I01L', 'I01M', 1227895110)
    instance0.fateNpcIds = Lists.newHashList(1865429068)
    instance0.mysteriousMonsterPos = vec2(-13540, 2330) 
    instance0.fateNpcPos = vec2(-12360, 2490) 
    
    // 副本2 崆峒山
    Instance instance1 = new Instance('I03V', 1, "崆峒山", 1, 300, 0, vec2(-15150, 5790))
    instance1.mysteriousMonsterIds = Lists.newHashList(1702258030, 1700884333, 1852007777)
    instance1.rareDropItemIds = Lists.newHashList(1227895109, 'I02S', 1227895111, 1227895371)
    instance1.fateNpcIds = Lists.newHashList(1865429071, 1865429059)
    instance1.mysteriousMonsterPos = vec2(-12635, 6600) 
    instance1.fateNpcPos = vec2(-13840, 8480)

    // 副本3 塞北
    Instance instance2 = new Instance('I04C', 2, "塞北", 2, 1000, 0, vec2(-15225, 10890))
    instance2.mysteriousMonsterIds = Lists.newHashList(1701672039, 1702061422, 1970169187)
    instance2.rareDropItemIds = Lists.newHashList('I02S', 1227895380, 1227895857)
    instance2.fateNpcIds = Lists.newHashList(1865429073)
    instance2.mysteriousMonsterPos = vec2(-13250, 14810)
    instance2.fateNpcPos = vec2(-15020, 14850)

    // 副本4 绝情谷
    Instance instance3 = new Instance('I04I', 3, "绝情谷", 3, 3000, 0, vec2(-15140, -6460))
    instance3.mysteriousMonsterIds = Lists.newHashList('nelb', 'nhfp', 'nmgr')
    instance3.rareDropItemIds = Lists.newHashList(1227895859, 'I042', 'I014')
    instance3.fateNpcIds = Lists.newHashList(1865429076, 'o026')
    instance3.mysteriousMonsterPos = vec2(-14880, -4820)
    instance3.fateNpcPos = vec2(-13000, -2580) 

    // 副本5 逍遥宫
    Instance instance4 = new Instance('I04V', 4, "逍遥宫", 4, 6000, 0, vec2(2180, 11930))
    instance4.mysteriousMonsterIds = Lists.newHashList(1853058166, 'nsbm')
    instance4.rareDropItemIds = Lists.newHashList('I00Q', 'I00S', 'I00R')
    instance4.fateNpcIds = Lists.newHashList(1865429080)
    instance4.mysteriousMonsterPos = vec2(4888, 14123) 
    instance4.fateNpcPos = vec2(3660, 13220) 

    // 副本6 黑木崖
    Instance instance5 = new Instance('I05L', 5, "黑木崖", 5, 10000, 0, vec2(7275, 11990)) 
    instance5.mysteriousMonsterIds = Lists.newHashList(1852142199, 1851942252, 1751672179)
    instance5.rareDropItemIds = Lists.newHashList('I00K', 'I00M')
    instance5.fateNpcIds = Lists.newHashList('crys')
    instance5.mysteriousMonsterPos = vec2(7250, 14710)
    instance5.fateNpcPos = vec2(7100, 13620) 