package KongFuLevelUp

import Hero

/* 武功升重系统 */



/**
 * 武功升重
 * @param coeff 武功升重系数
 */
public function kongFuLevelUp(unit u, int abilityId, int coeff)
    let level = u.getAbilityLevel(abilityId)
    let hero = u.getHero()
    var increment = (3 + hero.characterA) * (hero.perception + 5 + GetRandomInt(0, (coeff/60).toInt())) * 2
    // 慕容家训
    if u.hasAbility('B010')
        increment *= 5 + GetUnitAbilityLevel(u,'A02V') div 2
    else
        increment *= 4
    if level > 0 and level < 7
        hero.kongFuTable.saveInt(abilityId, hero.kongFuTable.loadInt(abilityId) + increment)
        hero.kongFuTable.saveString(abilityId * 2, hero.kongFuTable.loadInt(abilityId).toString() + "/" + (coeff * level).toString())
        if (hero.kongFuTable.loadInt(abilityId) >= coeff * level)
            levelUpAndShowToPlayers(u, abilityId)
    else if level > 0 and level < 9 and abilityId != 'A07W' // 乾坤大挪移只能升到七重
        if GetRandomInt(1, coeff * level) <= u.getHero().perception / 2 or (UnitHasBuffBJ(u, 'B010') and GetRandomInt(1, coeff * level) <= u.getHero().perception / 2 * (2 + 0.3 * u.getAbilityLevel('A02V')))
            levelUpAndShowToPlayers(u, abilityId)
        else
            if u.getHero().characterB >= 4 or u.hasItemById('I01J') 
                hero.kongFuTable.saveInt(abilityId, hero.kongFuTable.loadInt(abilityId) + increment)
                if (hero.kongFuTable.loadInt(abilityId) >= coeff * level)
                    levelUpAndShowToPlayers(u, abilityId)

/**
 * 提升技能等级并且展示给玩家
 */
function levelUpAndShowToPlayers(unit u, int abilityId)
    let hero = u.getHero()
    let i = u.getOwner().getId() + 1
    let level = u.getAbilityLevel(abilityId)
    hero.kongFuTable.saveInt(abilityId, 0)
    IncUnitAbilityLevel(u, abilityId)
    hero.kongFuTable.saveInt(abilityId * 5, u.getAbilityLevel(abilityId))
    DisplayTextToForce(bj_FORCE_ALL_PLAYERS,"|cff66ff00恭喜玩家" + i.toString() + "领悟了武功：" + GetObjectName(abilityId) + "第" + (level+1).toString() + "重")